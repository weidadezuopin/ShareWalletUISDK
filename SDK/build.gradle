plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.plugin.serialization' version '1.8.10'
    id 'maven-publish'
}

android {
    compileSdk 33

    defaultConfig {
        minSdk 21
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion = "1.4.2"
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    namespace 'com.sharedwallet.sdk'
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        // For Android libraries
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
    } else {
        // For pure Kotlin libraries, in case you have them
        from sourceSets.main.java.srcDirs
        from sourceSets.main.kotlin.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

version = '1.0.25'

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId = 'com.github.PeakPerformanceTechnology'
                artifactId = 'ShareWalletUISDK'
                version = this.version

                // Two artifacts, the `aar` (or `jar`) and the sources
                if (project.plugins.findPlugin("com.android.library")) {
                    artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
                } else {
                    artifact("$buildDir/libs/${project.getName()}-${version}.jar")
                }
                artifact androidSourcesJar

                pom {
                    name = 'SharedWallet'
                    description = 'SharedWallet for peak performances.'
                    url = 'https://peakperformances.io'
//                    signing {
//                        sign publishing.publications.release
////                        sign configurations.archives
//                    }
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'anastr'
                            name = 'Anas'
                            email = 'anas@peakperformances.io'
                        }
                    }
                    scm {
                        connection = 'scm:git:192.168.0.251/Developer_Android_Mattar/sharedwalledsdk.git'
                        developerConnection = 'scm:git:ssh://192.168.0.251/Developer_Android_Mattar/sharedwalledsdk.git'
                        url = 'http://192.168.0.251/Developer_Android_Mattar/sharedwalledsdk'
                    }
                    withXml {
                        def dependenciesNode = asNode().appendNode('dependencies')

                        project.configurations.implementation.allDependencies.each {
                            if (it.name != "ui-tooling-preview") {
                                def dependencyNode = dependenciesNode.appendNode('dependency')
                                dependencyNode.appendNode('groupId', it.group)
                                dependencyNode.appendNode('artifactId', it.name)
                                dependencyNode.appendNode('version', it.version)
                            }
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                name = "jitpack"
                url = uri("https://jitpack.io")
                credentials {
                    username = project.properties['authToken']
                }
            }
        }
    }
}

dependencies {
//    implementation files('/Users/peakperformance/Downloads/WalletSdkCore.aar')
    implementation 'com.github.PeakPerformanceTechnology:SharedWalletCoreSDK:V1.0.0@aar'

    implementation 'androidx.core:core-ktx:1.9.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.8.0'

    implementation 'androidx.activity:activity-compose:1.6.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-compose:2.6.0-beta01'

    implementation "io.insert-koin:koin-core:3.3.3"
    implementation "io.insert-koin:koin-android:3.3.3"
    implementation "io.insert-koin:koin-androidx-navigation:3.3.3"
    implementation "io.insert-koin:koin-androidx-compose:3.4.2"

    implementation "androidx.paging:paging-runtime:3.1.1"
    implementation "androidx.paging:paging-compose:1.0.0-alpha18"

    implementation "androidx.datastore:datastore-preferences:1.0.0"

    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.1"

    implementation "com.google.accompanist:accompanist-permissions:0.28.0"

    // CameraX
    implementation "androidx.camera:camera-camera2:1.2.0"
    implementation "androidx.camera:camera-lifecycle:1.2.0"
    implementation "androidx.camera:camera-view:1.2.0"

    // Zxing
    implementation 'com.google.zxing:core:3.5.1'

    implementation 'com.google.mlkit:barcode-scanning:17.0.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-play-services:1.6.4'

    def composeBom = platform('androidx.compose:compose-bom:2023.01.00')
    implementation composeBom
    implementation 'androidx.compose.material:material'
    implementation "androidx.compose.animation:animation:1.3.3"
    implementation "androidx.navigation:navigation-compose:2.5.3"
    implementation 'com.google.accompanist:accompanist-pager:0.28.0'
    implementation 'com.google.accompanist:accompanist-pager-indicators:0.28.0'

    // Android Studio Preview support
    implementation 'androidx.compose.ui:ui-tooling-preview'
    debugImplementation 'androidx.compose.ui:ui-tooling'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.0'
}